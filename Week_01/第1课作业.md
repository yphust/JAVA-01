- [第1课作业实践](#第1课作业实践)
  - [1.（可选）自己写一个简单地 Hello.java，里面需要涉及基本类型，四则运算，if和for，然后自己分析一下对应的字节码，有问题群里讨论。](#1可选自己写一个简单地-hellojava里面需要涉及基本类型四则运算if和for然后自己分析一下对应的字节码有问题群里讨论)
    - [a.代码](#a代码)
    - [b.字节码](#b字节码)
    - [c.解析](#c解析)
  - [2（必做）、自定义一个 Classloader，加载一个 Hello.xlass 文件，执行 hello 方法， 此文件内容是一个 Hello.class 文件所有字节（x=255-x）处理后的文件。文件群里提供。](#2必做自定义一个-classloader加载一个-helloxlass-文件执行-hello-方法-此文件内容是一个-helloclass-文件所有字节x255-x处理后的文件文件群里提供)
    - [a.代码](#a代码-1)
    - [b.运行截图](#b运行截图)
  - [3（必做）、画一张图，展示 Xmx、Xms、Xmn、Metaspace、DirectMemory、Xss这些内存参数的关系。](#3必做画一张图展示-xmxxmsxmnmetaspacedirectmemoryxss这些内存参数的关系)

## 第1课作业实践

### 1.（可选）自己写一个简单地 Hello.java，里面需要涉及基本类型，四则运算，if和for，然后自己分析一下对应的字节码，有问题群里讨论。

#### a.代码

```Java
package com.taiping.test;

/**
 * @author atom.hu
 * @version V1.0
 * @Package com.taiping.test
 * @date 2021/1/7 10:57
 */
public class HelloWorld {
    public static void main(String[] args) {
        int a = 0;
        int b = a + 1;
        double c = b * 3;
        String s = "a" + "b" + c;
        boolean bool = true;
        int[] arrs = {1,2,3,4,5};

        if(b > 0){
            System.out.println("Hello World!");
        }else{
            System.out.println("I'm" + bool);
        }

        for(int arr : arrs){
            System.out.println(arr);
        }
    }
}
```

#### b.字节码

```Java
Classfile /C:/Users/yanpeng.FT/IdeaProjects/practice/src/com/taiping/test/HelloWorld.class
  Last modified 2021-1-7; size 1234 bytes
  MD5 checksum 33e5340ab9918bbcc6aed571ffb13f67
  Compiled from "HelloWorld.java"
public class com.taiping.test.HelloWorld
  minor version: 0
  major version: 52
  flags: ACC_PUBLIC, ACC_SUPER
Constant pool:
   #1 = Methodref          #15.#45        // java/lang/Object."<init>":()V
   #2 = Class              #46            // java/lang/StringBuilder
   #3 = Methodref          #2.#45         // java/lang/StringBuilder."<init>":()V
   #4 = String             #47            // ab
   #5 = Methodref          #2.#48         // java/lang/StringBuilder.append:(Ljava/lang/String;)Ljava/lang/StringBuilder;
   #6 = Methodref          #2.#49         // java/lang/StringBuilder.append:(D)Ljava/lang/StringBuilder;
   #7 = Methodref          #2.#50         // java/lang/StringBuilder.toString:()Ljava/lang/String;
   #8 = Fieldref           #51.#52        // java/lang/System.out:Ljava/io/PrintStream;
   #9 = String             #53            // Hello World!
  #10 = Methodref          #54.#55        // java/io/PrintStream.println:(Ljava/lang/String;)V
  #11 = String             #56            // I'm
  #12 = Methodref          #2.#57         // java/lang/StringBuilder.append:(Z)Ljava/lang/StringBuilder;
  #13 = Methodref          #54.#58        // java/io/PrintStream.println:(I)V
  #14 = Class              #59            // com/taiping/test/HelloWorld
  #15 = Class              #60            // java/lang/Object
  #16 = Utf8               <init>
  #17 = Utf8               ()V
  #18 = Utf8               Code
  #19 = Utf8               LineNumberTable
  #20 = Utf8               LocalVariableTable
  #21 = Utf8               this
  #22 = Utf8               Lcom/taiping/test/HelloWorld;
  #23 = Utf8               main
  #24 = Utf8               ([Ljava/lang/String;)V
  #25 = Utf8               arr
  #26 = Utf8               I
  #27 = Utf8               args
  #28 = Utf8               [Ljava/lang/String;
  #29 = Utf8               a
  #30 = Utf8               b
  #31 = Utf8               c
  #32 = Utf8               D
  #33 = Utf8               s
  #34 = Utf8               Ljava/lang/String;
  #35 = Utf8               bool
  #36 = Utf8               Z
  #37 = Utf8               arrs
  #38 = Utf8               [I
  #39 = Utf8               StackMapTable
  #40 = Class              #28            // "[Ljava/lang/String;"
  #41 = Class              #61            // java/lang/String
  #42 = Class              #38            // "[I"
  #43 = Utf8               SourceFile
  #44 = Utf8               HelloWorld.java
  #45 = NameAndType        #16:#17        // "<init>":()V
  #46 = Utf8               java/lang/StringBuilder
  #47 = Utf8               ab
  #48 = NameAndType        #62:#63        // append:(Ljava/lang/String;)Ljava/lang/StringBuilder;
  #49 = NameAndType        #62:#64        // append:(D)Ljava/lang/StringBuilder;
  #50 = NameAndType        #65:#66        // toString:()Ljava/lang/String;
  #51 = Class              #67            // java/lang/System
  #52 = NameAndType        #68:#69        // out:Ljava/io/PrintStream;
  #53 = Utf8               Hello World!
  #54 = Class              #70            // java/io/PrintStream
  #55 = NameAndType        #71:#72        // println:(Ljava/lang/String;)V
  #56 = Utf8               I'm
  #57 = NameAndType        #62:#73        // append:(Z)Ljava/lang/StringBuilder;
  #58 = NameAndType        #71:#74        // println:(I)V
  #59 = Utf8               com/taiping/test/HelloWorld
  #60 = Utf8               java/lang/Object
  #61 = Utf8               java/lang/String
  #62 = Utf8               append
  #63 = Utf8               (Ljava/lang/String;)Ljava/lang/StringBuilder;
  #64 = Utf8               (D)Ljava/lang/StringBuilder;
  #65 = Utf8               toString
  #66 = Utf8               ()Ljava/lang/String;
  #67 = Utf8               java/lang/System
  #68 = Utf8               out
  #69 = Utf8               Ljava/io/PrintStream;
  #70 = Utf8               java/io/PrintStream
  #71 = Utf8               println
  #72 = Utf8               (Ljava/lang/String;)V
  #73 = Utf8               (Z)Ljava/lang/StringBuilder;
  #74 = Utf8               (I)V
{
  public com.taiping.test.HelloWorld();
    descriptor: ()V
    flags: ACC_PUBLIC
    Code:
      stack=1, locals=1, args_size=1
         0: aload_0
         1: invokespecial #1                  // Method java/lang/Object."<init>":()V
         4: return
      LineNumberTable:
        line 9: 0
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0       5     0  this   Lcom/taiping/test/HelloWorld;

  public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=4, locals=12, args_size=1
         0: iconst_0
         1: istore_1
         2: iload_1
         3: iconst_1
         4: iadd
         5: istore_2
         6: iload_2
         7: iconst_3
         8: imul
         9: i2d
        10: dstore_3
        11: new           #2                  // class java/lang/StringBuilder
        14: dup
        15: invokespecial #3                  // Method java/lang/StringBuilder."<init>":()V
        18: ldc           #4                  // String ab
        20: invokevirtual #5                  // Method java/lang/StringBuilder.append:(Ljava/lang/String;)Ljava/lang/StringBuilder;
        23: dload_3
        24: invokevirtual #6                  // Method java/lang/StringBuilder.append:(D)Ljava/lang/StringBuilder;
        27: invokevirtual #7                  // Method java/lang/StringBuilder.toString:()Ljava/lang/String;
        30: astore        5
        32: iconst_1
        33: istore        6
        35: iconst_5
        36: newarray       int
        38: dup
        39: iconst_0
        40: iconst_1
        41: iastore
        42: dup
        43: iconst_1
        44: iconst_2
        45: iastore
        46: dup
        47: iconst_2
        48: iconst_3
        49: iastore
        50: dup
        51: iconst_3
        52: iconst_4
        53: iastore
        54: dup
        55: iconst_4
        56: iconst_5
        57: iastore
        58: astore        7
        60: iload_2
        61: ifle          75
        64: getstatic     #8                  // Field java/lang/System.out:Ljava/io/PrintStream;
        67: ldc           #9                  // String Hello World!
        69: invokevirtual #10                 // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        72: goto          101
        75: getstatic     #8                  // Field java/lang/System.out:Ljava/io/PrintStream;
        78: new           #2                  // class java/lang/StringBuilder
        81: dup
        82: invokespecial #3                  // Method java/lang/StringBuilder."<init>":()V
        85: ldc           #11                 // String I'm
        87: invokevirtual #5                  // Method java/lang/StringBuilder.append:(Ljava/lang/String;)Ljava/lang/StringBuilder;
        90: iload         6
        92: invokevirtual #12                 // Method java/lang/StringBuilder.append:(Z)Ljava/lang/StringBuilder;
        95: invokevirtual #7                  // Method java/lang/StringBuilder.toString:()Ljava/lang/String;
        98: invokevirtual #10                 // Method java/io/PrintStream.println:(Ljava/lang/String;)V
       101: aload         7
       103: astore        8
       105: aload         8
       107: arraylength
       108: istore        9
       110: iconst_0
       111: istore        10
       113: iload         10
       115: iload         9
       117: if_icmpge     141
       120: aload         8
       122: iload         10
       124: iaload
       125: istore        11
       127: getstatic     #8                  // Field java/lang/System.out:Ljava/io/PrintStream;
       130: iload         11
       132: invokevirtual #13                 // Method java/io/PrintStream.println:(I)V
       135: iinc          10, 1
       138: goto          113
       141: return
      LineNumberTable:
        line 11: 0
        line 12: 2
        line 13: 6
        line 14: 11
        line 15: 32
        line 16: 35
        line 18: 60
        line 19: 64
        line 21: 75
        line 24: 101
        line 25: 127
        line 24: 135
        line 27: 141
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
          127       8    11   arr   I
            0     142     0  args   [Ljava/lang/String;
            2     140     1     a   I
            6     136     2     b   I
           11     131     3     c   D
           32     110     5     s   Ljava/lang/String;
           35     107     6  bool   Z
           60      82     7  arrs   [I
      StackMapTable: number_of_entries = 4
        frame_type = 255 /* full_frame */
          offset_delta = 75
          locals = [ class "[Ljava/lang/String;", int, int, double, class java/lang/String, int, class "[I" ]
          stack = []
        frame_type = 25 /* same */
        frame_type = 254 /* append */
          offset_delta = 11
          locals = [ class "[I", int, int ]
        frame_type = 248 /* chop */
          offset_delta = 27
}
SourceFile: "HelloWorld.java"
```

#### c.解析

```Java
Classfile /C:/Users/yanpeng.FT/IdeaProjects/practice/src/com/taiping/test/HelloWorld.class
  #编译时间及字节码文件大小  
  Last modified 2021-1-7; size 1234 bytes
  #MD5校验和
  MD5 checksum 33e5340ab9918bbcc6aed571ffb13f67
  #从哪个Java源文件编译而来
  Compiled from "HelloWorld.java"
#下面是类       
public class com.taiping.test.HelloWorld
#version是Java语言规范的版本 minor version是小的版本号，major version是大的版本号，52是JDK8
  minor version: 0
  major version: 52
  #访问标志符，ACC_PUBLIC表示这个类是public类，ACC_SUPER为历史BUG引入的    
  flags: ACC_PUBLIC, ACC_SUPER
#常量池 “#1”中数字表示常量的编号，该文件中其他地方可以引用。Methodref表明这个常量指向的是一个方法；具体是哪个类的哪个方法呢? 类指向的 #15 , 方法签名指向的 #45 ; 当然双斜线注释后面已经解析出来可读性比较好的说明了。Fieldref表明这个常量指向的是一个成员变量，class表明这个常量指向的是一个类，Uft8表明这个常量指向的是一个引用地址  
Constant pool:
   #1 = Methodref          #15.#45        // java/lang/Object."<init>":()V
   #2 = Class              #46            // java/lang/StringBuilder
   #3 = Methodref          #2.#45         // java/lang/StringBuilder."<init>":()V
   #4 = String             #47            // ab
   #5 = Methodref          #2.#48         // java/lang/StringBuilder.append:(Ljava/lang/String;)Ljava/lang/StringBuilder;
   #6 = Methodref          #2.#49         // java/lang/StringBuilder.append:(D)Ljava/lang/StringBuilder;
   #7 = Methodref          #2.#50         // java/lang/StringBuilder.toString:()Ljava/lang/String;
   #8 = Fieldref           #51.#52        // java/lang/System.out:Ljava/io/PrintStream;
   #9 = String             #53            // Hello World!
  #10 = Methodref          #54.#55        // java/io/PrintStream.println:(Ljava/lang/String;)V
  #11 = String             #56            // I'm
  #12 = Methodref          #2.#57         // java/lang/StringBuilder.append:(Z)Ljava/lang/StringBuilder;
  #13 = Methodref          #54.#58        // java/io/PrintStream.println:(I)V
  #14 = Class              #59            // com/taiping/test/HelloWorld
  #15 = Class              #60            // java/lang/Object
  #16 = Utf8               <init>
  #17 = Utf8               ()V
  #18 = Utf8               Code
  #19 = Utf8               LineNumberTable
  #20 = Utf8               LocalVariableTable
  #21 = Utf8               this
  #22 = Utf8               Lcom/taiping/test/HelloWorld;
  #23 = Utf8               main
  #24 = Utf8               ([Ljava/lang/String;)V
  #25 = Utf8               arr
  #26 = Utf8               I
  #27 = Utf8               args
  #28 = Utf8               [Ljava/lang/String;
  #29 = Utf8               a
  #30 = Utf8               b
  #31 = Utf8               c
  #32 = Utf8               D
  #33 = Utf8               s
  #34 = Utf8               Ljava/lang/String;
  #35 = Utf8               bool
  #36 = Utf8               Z
  #37 = Utf8               arrs
  #38 = Utf8               [I
  #39 = Utf8               StackMapTable
  #40 = Class              #28            // "[Ljava/lang/String;"
  #41 = Class              #61            // java/lang/String
  #42 = Class              #38            // "[I"
  #43 = Utf8               SourceFile
  #44 = Utf8               HelloWorld.java
  #45 = NameAndType        #16:#17        // "<init>":()V
  #46 = Utf8               java/lang/StringBuilder
  #47 = Utf8               ab
  #48 = NameAndType        #62:#63        // append:(Ljava/lang/String;)Ljava/lang/StringBuilder;
  #49 = NameAndType        #62:#64        // append:(D)Ljava/lang/StringBuilder;
  #50 = NameAndType        #65:#66        // toString:()Ljava/lang/String;
  #51 = Class              #67            // java/lang/System
  #52 = NameAndType        #68:#69        // out:Ljava/io/PrintStream;
  #53 = Utf8               Hello World!
  #54 = Class              #70            // java/io/PrintStream
  #55 = NameAndType        #71:#72        // println:(Ljava/lang/String;)V
  #56 = Utf8               I'm
  #57 = NameAndType        #62:#73        // append:(Z)Ljava/lang/StringBuilder;
  #58 = NameAndType        #71:#74        // println:(I)V
  #59 = Utf8               com/taiping/test/HelloWorld
  #60 = Utf8               java/lang/Object
  #61 = Utf8               java/lang/String
  #62 = Utf8               append
  #63 = Utf8               (Ljava/lang/String;)Ljava/lang/StringBuilder;
  #64 = Utf8               (D)Ljava/lang/StringBuilder;
  #65 = Utf8               toString
  #66 = Utf8               ()Ljava/lang/String;
  #67 = Utf8               java/lang/System
  #68 = Utf8               out
  #69 = Utf8               Ljava/io/PrintStream;
  #70 = Utf8               java/io/PrintStream
  #71 = Utf8               println
  #72 = Utf8               (Ljava/lang/String;)V
  #73 = Utf8               (Z)Ljava/lang/StringBuilder;
  #74 = Utf8               (I)V
{
 #空参构造函数
  public com.taiping.test.HelloWorld();
  #方法描述：其中小括号内是入参信息/形参信息,这里表示是没有入参，小括号后面的 V 则表示这个方法的返回值是 void
    descriptor: ()V
    #方法的访问标识符，ACC_PUBLIC表示public
    flags: ACC_PUBLIC
    Code:
    #执行该方法时需要的栈(stack)深度是1，需要在局部变量表中保留1个槽位, 还有方法的参数个数为1
      stack=1, locals=1, args_size=1
      #从局部变量0中装载引用类型值入栈，这里就this，HelloWorld
         0: aload_0
         #调用编号1的方法，即Object类的构造方法
         1: invokespecial #1                  // Method java/lang/Object."<init>":()V
         #void函数返回
         4: return
       #LineNumberTable 该属性的作用是描述源码行号与字节码行号(字节码偏移量)之间的对应关系。可以使用 -g:none 或-g:lines选项来取消或要求生成这项信息，如果选择不生成LineNumberTable，当程序运行异常时将无法获取到发生异常的源码行号，也无法按照源码的行数来调试程序。       
      LineNumberTable:
        line 9: 0
      #LocalVariableTable 该属性的作用是描述帧栈中局部变量与源码中定义的变量之间的关系。可以使用 -g:none 或 -g:vars来取消或生成这项信息，如果没有生成这项信息，那么当别人引用这个方法时，将无法获取到参数名称，取而代之的是arg0, arg1这样的占位符。 start 表示该局部变量在哪一行开始可见，length表示可见行数，Slot代表所在帧栈位置，Name是变量名称，然后是类型签名。    
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0       5     0  this   Lcom/taiping/test/HelloWorld;
#	下面是main方法体
  public static void main(java.lang.String[]);
  #([Ljava/lang/String;)V 其中小括号内是入参信息/形参信息,左方括号表述数组,L 表示对象,后面的 java/lang/String 就是类名称，小括号后面的 V 则表示这个方法的返回值是 void
    descriptor: ([Ljava/lang/String;)V
    #flags: ACC_PUBLIC, ACC_STATIC ，表示public和static
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
    #执行该方法时需要的栈(stack)深度是4，需要在局部变量表中保留12个槽位, 还有方法的参数个数是1
      stack=4, locals=12, args_size=1
      	 #0(int)值入栈
         0: iconst_0
         #将栈顶int类型值保存到局部变量1中。
         1: istore_1
         #从局部变量1中装载int类型值入栈。
         2: iload_1
         #1(int)值入栈。
         3: iconst_1
         #将栈顶两int类型数相加，结果入栈。
         4: iadd
         #将栈顶int类型值保存到局部变量2中。
         5: istore_2
         #从局部变量2中装载int类型值入栈。
         6: iload_2
         #3(int)值入栈
         7: iconst_3
         #将栈顶两int类型数相乘，结果入栈。
         8: imul
         #将栈顶int类型值转换为double类型值。
         9: i2d
         #将栈顶double类型值保存到局部变量3中。
        10: dstore_3
        #创建新的对象实例。 此处是由String的字符串拼接引起的
        11: new           #2                  // class java/lang/StringBuilder
        #复制栈顶一个字长的数据，将复制后的数据压栈。
        14: dup
        #调用StringBuilder的构造方法
        15: invokespecial #3                  // Method java/lang/StringBuilder."<init>":()V
        #常量池中的常量值（int, float, string reference, object reference）入栈。 此处是ab
        18: ldc           #4                  // String ab
        #调用StringBuilder的append方法拼接字符串
        20: invokevirtual #5                  // Method java/lang/StringBuilder.append:(Ljava/lang/String;)Ljava/lang/StringBuilder;
        #从局部变量3中装载double类型值入栈。 变量c
        23: dload_3
        #调用StringBuilder的append方法拼接字符串，拼接c
        24: invokevirtual #6                  // Method java/lang/StringBuilder.append:(D)Ljava/lang/StringBuilder;
        #调用StringBuilder.toString转成String
        27: invokevirtual #7                  // Method java/lang/StringBuilder.toString:()Ljava/lang/String;
        #将栈顶引用类型值保存到局部变量5中。
        30: astore        5
        #1(int)值入栈 布尔值true就是1
        32: iconst_1
        #将栈顶引用类型值保存到局部变量6中。
        33: istore        6
        #5(int)值入栈
        35: iconst_5
        #创建int类型的数组。
        36: newarray       int
        #复制栈顶一个字长的数据，将复制后的数据压栈。
        38: dup
        #0(int)值入栈
        39: iconst_0
        #1(int)值入栈
        40: iconst_1
        #将栈顶int类型值保存到指定int类型数组的指定项。即int 1放到数字0号位
        41: iastore
        #复制栈顶一个字长的数据，将复制后的数据压栈。
        42: dup
        #1(int)值入栈
        43: iconst_1
        #2(int)值入栈
        44: iconst_2
        #将栈顶int类型值保存到指定int类型数组的指定项。即int 2放到数字1号位
        45: iastore
        #复制栈顶一个字长的数据，将复制后的数据压栈。
        46: dup
        #2(int)值入栈
        47: iconst_2
        #3(int)值入栈
        48: iconst_3
        #将栈顶int类型值保存到指定int类型数组的指定项。即int 3放到数字2号位
        49: iastore
        #复制栈顶一个字长的数据，将复制后的数据压栈。
        50: dup
        #3(int)值入栈
        51: iconst_3
        #4(int)值入栈
        52: iconst_4
        #将栈顶int类型值保存到指定int类型数组的指定项。即int 4放到数字3号位
        53: iastore
        #复制栈顶一个字长的数据，将复制后的数据压栈。
        54: dup
        #4(int)值入栈
        55: iconst_4
        #5(int)值入栈
        56: iconst_5
        #将栈顶int类型值保存到指定int类型数组的指定项。即int 5放到数字4号位
        57: iastore
        #将栈顶引用类型值保存到局部变量7中。
        58: astore        7
        #从局部变量2中装载int类型值入栈。
        60: iload_2
        #若栈顶int类型值小于等于0则跳转到偏移量75的位置（实际b=1,不会跳转，if为真）
        61: ifle          75
        #获取静态字段的值。此处为获取输出流
        64: getstatic     #8                  // Field java/lang/System.out:Ljava/io/PrintStream;
        #常量池中的常量值（int, float, string reference, object reference）入栈。
        67: ldc           #9                  // String Hello World!
        #调用PrintStream.println打印Hello World!
        69: invokevirtual #10                 // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        #这里是else，因为不满足，所以直接跳转到偏移量101的位置
        72: goto          101
        75: getstatic     #8                  // Field java/lang/System.out:Ljava/io/PrintStream;
        78: new           #2                  // class java/lang/StringBuilder
        81: dup
        82: invokespecial #3                  // Method java/lang/StringBuilder."<init>":()V
        85: ldc           #11                 // String I'm
        87: invokevirtual #5                  // Method java/lang/StringBuilder.append:(Ljava/lang/String;)Ljava/lang/StringBuilder;
        90: iload         6
        92: invokevirtual #12                 // Method java/lang/StringBuilder.append:(Z)Ljava/lang/StringBuilder;
        95: invokevirtual #7                  // Method java/lang/StringBuilder.toString:()Ljava/lang/String;
        98: invokevirtual #10                 // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        #下面是for循环
       #从局部变量7装载引用类型值入栈
       101: aload         7
       #将栈顶引用类型值保存到局部变量8中
       103: astore        8
       #从局部变量8装载引用类型值入栈
       105: aload         8
       #获取一维数组的长度。
       107: arraylength
       #将栈顶int类型值保存到局部变量9中。
       108: istore        9
       #0(int)值入栈。
       110: iconst_0
       #将栈顶int类型值保存到局部变量10中。
       111: istore        10
       #从局部变量10中装载int类型值入栈。
       113: iload         10
       #从局部变量9中装载int类型值入栈。
       115: iload         9
       #若栈顶两int类型值前大于等于后则跳转。此处是判断循环是否结束
       117: if_icmpge     141
       #从局部变量8装载引用类型值入栈
       120: aload         8
       #从局部变量10中装载int类型值入栈。
       122: iload         10
       #从int类型数组中装载指定项的值。
       124: iaload
       ##将栈顶int类型值保存到局部变量11中。
       125: istore        11
       #获取静态字段的值。此处为获取输出流
       127: getstatic     #8                  // Field java/lang/System.out:Ljava/io/PrintStream;
       #从局部变量11中装载int类型值入栈。
       130: iload         11
       #调用PrintStream.println打印当前循环的数组元素
       132: invokevirtual #13                 // Method java/io/PrintStream.println:(I)V
       #将整数值1加到10指定的int类型的局部变量中。
       135: iinc          10, 1
       #跳转回113偏移量的位置
       138: goto          113
       #数组遍历结束，返回
       141: return
       #LineNumberTable  左边的列是源代码的行数 右边的列是字节码的偏移量
      LineNumberTable:
        line 11: 0
        line 12: 2
        line 13: 6
        line 14: 11
        line 15: 32
        line 16: 35
        line 18: 60
        line 19: 64
        line 21: 75
        line 24: 101
        line 25: 127
        line 24: 135
        line 27: 141
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
          127       8    11   arr   I
            0     142     0  args   [Ljava/lang/String;
            2     140     1     a   I
            6     136     2     b   I
           11     131     3     c   D
           32     110     5     s   Ljava/lang/String;
           35     107     6  bool   Z
           60      82     7  arrs   [I
      StackMapTable: number_of_entries = 4
        frame_type = 255 /* full_frame */
          offset_delta = 75
          locals = [ class "[Ljava/lang/String;", int, int, double, class java/lang/String, int, class "[I" ]
          stack = []
        frame_type = 25 /* same */
        frame_type = 254 /* append */
          offset_delta = 11
          locals = [ class "[I", int, int ]
        frame_type = 248 /* chop */
          offset_delta = 27
}
SourceFile: "HelloWorld.java"
```

### 2（必做）、自定义一个 Classloader，加载一个 Hello.xlass 文件，执行 hello 方法， 此文件内容是一个 Hello.class 文件所有字节（x=255-x）处理后的文件。文件群里提供。

#### a.代码

```Java
package Week_01;

import sun.misc.BASE64Encoder;

import java.io.*;
import java.util.Base64;

/**
 * @author yanpeng
 * @version V1.0
 * @Package Week_01
 * @date 2021/1/7 16:22
 */
public class HelloClassLoader extends ClassLoader {
    public static void main(String[] args) {
       try {
         Object instance = new HelloClassLoader().findClass("Hello").newInstance();
         instance.getClass().getMethod("hello").invoke(instance);
        }  catch (Exception e) {
            e.printStackTrace();
        }
    }

    @Override
    protected Class<?> findClass(String name) throws ClassNotFoundException {
        String helloBase64 = getBase64Str("Hello.xlass");

        byte[] bytes = decode(helloBase64);
        return defineClass(name,bytes,0,bytes.length);
    }

    /**
     * 读取指定文件并转成Base64字符
     * @param fileName
     * @return
     */
    private String getBase64Str(String fileName) {
        String projectPath = System.getProperty("user.dir");
        String packageName = this.getClass().getPackage().getName();
        String packagePath = File.separator + "src" + File.separator + packageName;

        InputStream inputStream = null;
        byte[] buffer = null;
        try {
            inputStream = new FileInputStream(projectPath + packagePath + File.separator + fileName);
            int count = 0;
            while (count == 0) {
                count = inputStream.available();
            }
            buffer = new byte[count];
            inputStream.read(buffer);
        } catch (IOException e) {
            e.printStackTrace();
        }finally {
            if (inputStream != null) {
                try {
                    // 关闭inputStream流
                    inputStream.close();
                } catch (IOException e) {
                    e.printStackTrace();
                }
            }
        }
        // 对字节数组Base64编码
        return new BASE64Encoder().encode(buffer);
    }

    /**
     * 对Base64码先解码，然后根据原始移位规则用255减去每一个字节，得到原始字节码
     * @param base64
     * @return
     */
    private byte[] decode(String base64) {
        byte[] bytes = Base64.getMimeDecoder().decode(base64);
        for(int i = 0; i < bytes.length; i++){
            bytes[i] = (byte) (255 - bytes[i]);
        }
        return bytes;
    }
}
```

#### b.运行截图

[![sm4gzT.png](https://s3.ax1x.com/2021/01/07/sm4gzT.png)](https://imgchr.com/i/sm4gzT)

### 3（必做）、画一张图，展示 Xmx、Xms、Xmn、Metaspace、DirectMemory、Xss这些内存参数的关系。

